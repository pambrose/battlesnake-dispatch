plugins {
  id 'java'
  id 'org.jetbrains.kotlin.jvm' version '1.3.71'
  id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.71'
  id "com.github.ben-manes.versions" version "0.28.0"
}

repositories {
  maven { url "https://kotlin.bintray.com/kotlinx" }
  maven { url = 'https://maven-central.storage-download.googleapis.com/repos/central/data/' }
  jcenter()
}

def junitVersion = '5.6.1'
def kluentVersion = '1.60'
def loggingVersion = '1.7.9'
def serializationVersion = '0.20.0-1.3.70-eap-274-2'
def slf4jVersion = '1.7.28'
def sparkVersion = '2.9.1'

sourceCompatibility = 1.8

description = 'battlesnake-quickstart'
group = 'io.battlesnake'
version = '0.3.4'

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "org.jetbrains.kotlin:kotlin-reflect"

  implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${serializationVersion}"
  implementation "io.github.microutils:kotlin-logging:${loggingVersion}"
  implementation "com.sparkjava:spark-core:${sparkVersion}"
  implementation "org.slf4j:slf4j-simple:${slf4jVersion}"

  testImplementation "org.amshove.kluent:kluent:${kluentVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

compileKotlin {
  kotlinOptions.jvmTarget = '1.8'
  kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                                     '-Xuse-experimental=kotlinx.serialization.UnstableDefault']
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '1.8'
  kotlinOptions.freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                                     '-Xuse-experimental=kotlinx.serialization.UnstableDefault']
}

test {
  useJUnitPlatform()

  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    exceptionFormat "full"
    showStandardStreams = true
  }
}